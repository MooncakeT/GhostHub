//---------------------------Mouse Functions--------------------------
//As you've probably guessed, this .dic file has to do with mouse functions. This includes the petting feature and what happens when you double click your ghost.


//--OnMouseMove
//This is a bunch of code determining who the mouse is over. Don't touch any of it.

OnMouseMove
 {
		//---- reference3 determines the character in question
		if reference3 == 0 //if the character is 0, aka the main character
		{
			//---- it will run the mouse functions for that character
			MouseMoveSakura
		}
		else//if the character isn't 0, then it must be the side character.
		{
			if reference3 == 1{//---- this will run the mouse functions for the side character.
				MouseMoveKero
			}else{
				MouseMoveSan
			}
		}
		
 }
 
 //*********************Advanced User Info*******************************
//If you want to have more than two characters at any point, like for example how I added Charger with my Hunter/Smoker ghost, and you want to be able to interact with that character like you can with the others, you will have to add in that character to OnMouseMove above. You'll need to know the p[] you've assigned your third character to, which in Charger's case was 3. You can assign your third character any p you want, just be consistent about using the same p[] everytime. I touch on \p a bit more in the walkthrough.
//So anyway, as you can see above, OnMouseMove determines who the user is touching, so you'll just have to add in an elseif checking for reference3 == 3, or reference3 == 4, or whatever p number is you're using. After that, you'll just need to copy and paste the other mouse functions below and change the name as you need (for example, MouseMoveCharger, MouseDoubleClickCharger). 

//Note though that the extra character will not be a hard-coded character like 0 and 1! You're basically tricking the program into thinking there's another character, and some functions in things like menu.dic and aitalk.dic won't work quite right if you put in a value other than 0 or 1, and you'll need to be careful about making sure to remove the third character when you're done. But luckily for you, this won't be a problem with any of the mouse functions as far as I'm aware, and you can fill them out for the third character just like you would for the two main ones.
//***********************************************************************


 //--MouseMoveSakura
 //The petting function for your main character.
 
MouseMoveSakura
 {
	if reference4 != "" //ignore
 	{
 		stroke++ //if you move the mouse over the right areas, the stroke count will go up.
 		if stroke >= 40 //if the stroke count is above a certain number, the petting dialogue will kick in. You can change this number to make the petting take longer or shorter if you want. FLELEs have it set to 50 for some reason, which makes it take longer than this.
		{
 			stroke = 0 //resetting the stroke count
 			wakPets += 1
			if reference4 == "Head" //This is where you'd put in the name of your collision, such as head, face, horns, tail, ears, whatever.
 			{
				"\0\s[1]That's nice.\w8\w8\w8\s[3] Don't take my hat though.\e"
				"\0\s[1]How sweet of you!\w8\w6 Like a big frosted chocolate donut.\e"
				"\0\s[6]*Licks you.*\e"
 			}
 		}
	}
 	else
	{
 		// If the mouse isn't over the ghost, it'll zero out the stroke value.
		stroke = 0
 	}
}

 //---- MouseMoveKero -----------------------------------------------------------------
 //MouseMoveKero is exactly like the above, except set up for your second character. 
 
MouseMoveKero
 {
	if reference4 != ""
 	{
 		stroke++
 		if stroke >= 40
		{
			stroke = 0
			yakPets += 1
			if reference4 == "Head"
 			{
				"\1\s[11]Aw, thanks!\e"
				"\1\s[11]You're so nice.\e"
				"\1\s[11]I know, I know.\w5\s[13] I \f[italic,1]am\f[italic,0] devishilly handsome.\e"
 			}	
 		}
	}
 	else
	{
 		// If the mouse isn't over the ghost, it'll zero out the stroke value.
		stroke = 0
 	}
}


//---- MouseMoveSan -----------------------------------------------------------------
 //MouseMoveSan is exactly like the above, except set up for your third character. 

MouseMoveSan
 {
	if reference4 != ""
 	{
 		stroke++
 		if stroke >= 40
		{
			stroke = 0
			dotPets += 1
			if reference4 == "Head"
 			{
				"\p[2]Well aren't you a sweetheart!\e"
				"\p[2]\s[24]Careful of the flower, please.\e"
				"\p[2]I know, I'm cute.\e"
 			}	
 		}
	}
 	else
	{
 		// If the mouse isn't over the ghost, it'll zero out the stroke value.
		stroke = 0
 	}
}



//---- OnMouseDoubleClick -------------------------------------------------------
//This is set up much like OnMouseMove above, except this is checking to see if the user is double clicking on the ghost.

OnMouseDoubleClick
 {
	//---- like with the OnMouseMove function above, this determines who the user is clicking on.
	if reference5 == 0 //checks which button is being used for the click
	{
	 	if reference3 == 0 //if the character is 0, aka your main character
		{
			MouseDoubleClickSakura
		}
		else
		{
 			if reference3 == 1{
 				MouseDoubleClickKero
 			}else{
 				MouseDoubleClickSan
 			}
		}
	}
 }

 //---- MouseDoubleClickSakura ----------------------------------------------------------
 //Like the petting animation above, this is the reactions they have to being double clicked.
 
 
MouseDoubleClickSakura
 {
 		OpenSakuraMenu
 }

 
 //---- MouseDoubleClickKero -----------------------------------------------------------
 //This is just the same as MouseDoubleClickSakura above, except for your secondary character.
 
MouseDoubleClickKero
 {
		OpenKeroMenu //This will open his menu set up in menu.dic.
 }


  //---- MouseDoubleClickSan -----------------------------------------------------------
 //This is just the same as MouseDoubleClickSakura above, except for your tertiary character.
 
MouseDoubleClickSan
 {
		OpenSanMenu //This will open his menu set up in menu.dic.
 }

 //*********************Advanced User Info*******************************
 //There are other mouse related functions you could set up much like the above, such as OnMouseDragStart, OnMouseDragEnd, OnMouseWheel, and others which correspond to various things you can do with your mouse. You can probably guess what those functions do just by their names - OnMouseWheel would be if someone spun their mouse wheel over a certain area, for example. If you're interested in these, I'd recommend doing some reading at the CROW-SSP Encyclopedia to get a better idea of them and settle down for some trial and error learning as you try to put them in.
 //***********************************************************************