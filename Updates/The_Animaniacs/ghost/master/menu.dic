//---------------------------Menus--------------------------
//This .dic file is primarily concerned with the double-click menus for each of your ghosts.

//Functions in this file

//OpenSakuraMenu
//MenuRun
//OnLastTalk
//GetWeekDay
//GetAP
//Select.BACKTOMENU
//Select.configmenu
//TalkConfig
//Pronoun stuff
//Delete on/off
//Bithday stuff
//Talk Rate

//--OpenSakuraMenu
//This is a bit of intro dialogue before the main character opens their menu.
//The \b2 tag tells the ghost to use their big balloon for this dialogue.
//If you don't want multiple responses before opening the menu, you can delete the dialogue and just leave MenuRun.

OpenSakuraMenu
{
		"\0\s[0]\b2Yes?"
		"\0\s[0]\b2Is it snack time?"
		"\0\s[0]\b2Do you need something?"
		--
		MenuRun
}

//--MenuRun
//As you just saw above. This actually fills out the rest of the menu.

MenuRun
 {	
 	wd = GetWeekDay
 	ap = GetAP
	//These four statements basically get the day of the week, whether it's AM or PM, the hour, and the time. It's putting together the information to display the time at the bottom of the balloon.
	--
	"\n\n\_q\b2" //\_q means quick session, meaning all the text from this point on will display instantly instead of a few letters at a time like normal ghost dialogue.
	--
	if lastTalk != ""
	{
	  "\![*]\q[Repeat Last Dialogue,OnLastTalk]\n\n[half]"
	}
	--
 	"\![*]\q[Say something,OnAiTalk]\n"
	"\![*]\q[Say something 2,OnAiTalk]\n"
	
	//This option will force the ghost to say something from aitalk if the user clicks it.
	--
	"\![*]\q[Talk,wakkoTalk]\n\n[half]/
	\![*]\q[Config Menu,configmenu]\n/
	\![*]\q[Functions,dosomething]\n\n[half]/
 	\![*]\q[Nevermind,CANCEL]\n\n\n[half]%(wd) %(nowhour):%(nowmin) %(ap), %(month)/%(day)/%(year)\e"
}

//Leave this alone
OnLastTalk 
{
"%(lastTalk)\e"
}

//This is figuring out what day of the week it is. Leave it alone.
GetWeekDay
{
	if weekday == 0
	{"Sun"}
	elseif weekday == 1
	{"Mon"}
	elseif weekday == 2
	{"Tue"}
	elseif weekday == 3
	{"Wed"}
	elseif weekday == 4
	{"Thu"}
	elseif weekday == 5
	{"Fri"}
	else
	{"Sat"}
}

//This is finding out whether it's AM or PM. Leave it alone.
GetAP
{
 	if hour >= 0 && hour <= 11
 		{"AM"}
 	else
 		{"PM"}
}

nowhour
{
	if hour12 == 0; "1"
	else; "%(hour12)"
}

nowmin
{
	if minute >= 0 && minute <=9; "0%(minute)"
	else; "%(minute)"
}


//If someone selects an option linked to BACKTOMENU, it will go back to MenuRun.
Select.BACKTOMENU
	{
		MenuRun
	}

//If someone selects configmenu, this will lead to TalkConfig.
Select.configmenu
	{
		TalkConfig
	}

//--TalkConfig
//This menu allows the user to change their name and other information, as well as put in their birthday and change how often the ghost speaks.

TalkConfig
{
	"\0\b[2]\_q\" 
	//This bit of dialogue sets up a large balloon and a quick session, like above, so all the text will appear immediately.
	--
	"f[bold,1]Your name:\f[bold,0] %(username)    \f[italic,1](\q[Change it?,TEACHNAME])\f[italic,0]\n"
	--
	"\f[bold,1]Your pronouns:\f[bold,0] %(heshe)/%(himher)/%(hisher)    \f[italic,1](\q[Change them?,TEACHPRONOUN])\f[italic,0]\n"
	--
	"\f[bold,1]Your birthday:\f[bold,0] %(birthdayprint)    \f[italic,1](\q[Change it?,TEACHBDAY])\f[italic,0]\n\n"
	--
	"\f[bold,1]Talk Rate:\f[bold,0] - every %(talktime)    \f[italic,1](\q[Change it,talkinter])\f[italic,0]\n\n"
	--		
		{
		"\![*]Enable Uninstall? (NOW : %(deleteshitai))\n\q[ON,deleteon] \q[OFF,deleteoff]\n\n[half]\n"
		}
	--
	"\![*]\q[Back to the menu,BACKTOMENU]\e"
}

//This and the next few set up the pronouns, as you can see by the linkedfunction names.

Select.TEACHPRONOUN
{
	"\0\s[0]What pronouns do you want?\w5\n\n[half]\_q/
		\![*]\q[He-His-Him,choicemasc]\n/
		\![*]\q[She-Her-Her,choicefem]\n/	
		\![*]\q[They-Their-Them,choiceneither]\e"
}

Select.choicemasc
{
	presuffix = "masculine"
	himher = "him"
	heshe = "he"
	hisher = "his"
	hesshes = "he's"
	--
	"\0\s[0]The pronouns are now he him and his.\e"
}

Select.choicefem
{
	presuffix = "feminine"
	himher = "her"
	heshe = "she"
	hisher = "her"
	hesshes = "she's"
	--
	"\0\s[0]The pronouns are now she her and her.\e"
}

Select.choiceneither
{
	presuffix = "undefine"
	himher = "them"
	heshe = "they"
	hisher = "their"
	hesshes = "they're"
	--
	"\0\s[0]The pronouns are now they them and their.\e"
}

//Here are some examples of these envelopes in use.
//"%(heshe) left that there."
//"%(hisher) work is really cool."
//"I'll ask %(himher) later."
//"%(hesshes) really good at it."

//These two options toggle whether or not you can delete your ghost. Leave them alone.

Select.deleteon
{
	deleteshitai = "ON"
	TalkConfig
}

Select.deleteoff
{
	deleteshitai = "OFF"
	TalkConfig
}


//This next set of functions handles inputting the user's birthday.

Select.TEACHBDAY
	{	
		"\0\s[0]What month were you born? \w8Name or number is fine.\![open,inputbox,OnUserBornMonth,-1]\e"
		
		//You can replace the dialogue up to the \![open,inputbox,OnUserBornMonth,-1] tag, leave that alone. You'll need the input box to get the information.
	}

//--OnUserBornMonth
//This takes the value that the user put into the input box above and evaluates it. Most of this you won't have to touch, but I'll explain what it's doing anyway.

OnUserBornMonth
{
	_tempmonth = TOSTR(reference0) //this stores what the user put in as a variable called _tempmonth
	_tempmonth = TOLOWER(_tempmonth) //changes it to lowercase
	
	//this is attaching the right value to the input from the user
	if _tempmonth == "january" || _tempmonth == "1" {bornmonth = 1; bornmonthprint = "January"}
	elseif _tempmonth == "february" || _tempmonth == "2" {bornmonth = 2; bornmonthprint = "February"}
	elseif _tempmonth == "march" || _tempmonth == "3" {bornmonth = 3; bornmonthprint = "March"}
	elseif _tempmonth == "april" || _tempmonth == "4" {bornmonth = 4; bornmonthprint = "April"}
	elseif _tempmonth == "may" || _tempmonth == "5" {bornmonth = 5; bornmonthprint = "May"}
	elseif _tempmonth == "june" || _tempmonth == "6" {bornmonth = 6; bornmonthprint = "June"}
	elseif _tempmonth == "july" || _tempmonth == "7" {bornmonth = 7; bornmonthprint = "July"}
	elseif _tempmonth == "august" || _tempmonth == "8" {bornmonth = 8; bornmonthprint = "August"}
	elseif _tempmonth == "september" || _tempmonth == "9" {bornmonth = 9; bornmonthprint = "September"}
	elseif _tempmonth == "october" || _tempmonth == "10" {bornmonth = 10; bornmonthprint = "October"}
	elseif _tempmonth == "november" || _tempmonth == "11" {bornmonth = 11; bornmonthprint = "November"}
	elseif _tempmonth == "december" || _tempmonth == "12" {bornmonth = 12; bornmonthprint = "December"}
	
	else //if the month isn't a valid month, like 32 or something.
	{
			"\0\s[0]That's not a valid month. Try again."
			--
			"\![open,inputbox,OnUserBornMonth,-1]\![set,balloontimeout,-1]\e"
	}
	--
	//If it's a valid month
	
	"\0\s[0]So you were born in %(bornmonthprint).\n\w8What day was it?"
	--
	"\![open,inputbox,OnUserBornDay,-1]\![set,balloontimeout,-1]\e"
	
	//Here you can see the use of a bornmonthprint envelope to show the user what month they put in. Notice that it opens an inputbox at the end the same way the one above did.
}

//This adds an appropriate suffix to the number for the day. No need to mess with this.

Printday
{
	if bornday == 1 || bornday == 21 || bornday == 31
	{	"%(bornday)st"	}
	elseif bornday == 2 || bornday == 22
	{	"%(bornday)nd"	}
	elseif bornday == 3 || bornday == 23
	{	"%(bornday)rd"	}
	else
	{	"%(bornday)th"	}
}	

//--OnUserBornDay
//This is much like the above function OnUserBornMonth. It'll take the value the user put in for the day and analyze it. 

OnUserBornDay
{
	_tempday = reference0 //storing the input in _tempday
	_tempday = TOLOWER(_tempday) //changes it to lowercase
	_tempday = REPLACE(_tempday,"st","") //if the user types something like 2nd, 3rd, etc, this will make it just the number. It'll just add the suffix back later on the one that actually shows.
	_tempday = REPLACE(_tempday,"nd","")
	_tempday = REPLACE(_tempday,"rd","")
	_tempday = REPLACE(_tempday,"th","")
	_tempday = TOINT(_tempday) //Converts it to an integer
	
	if ((bornmonth == 1 || bornmonth == 3 || bornmonth == 5 || bornmonth == 7 || bornmonth == 8 || bornmonth == 10 || bornmonth == 12) && _tempday >= 1 && _tempday <= 31) || (bornmonth == 2 && _tempday >= 1 && _tempday <= 29) || ((bornmonth == 4 || bornmonth == 6 || bornmonth == 9 || bornmonth == 11) && _tempday >= 1 && _tempday <= 30) //This ugly function checks if the day falls into a valid range. If it does, we set some variables and continue down.
	{
		bornday = _tempday
		borndayprint = Printday
		userbirthday = "%(bornmonth)month %(bornday)day"
		birthdayprint = "%(bornmonthprint) %(borndayprint)"
		
		"%(birthchecked)"
		//Mostly defining envelopes and values in here. Eagle-eyed readers will notice the new envelope birthchecked on its own line like a tiny king.
	}
	else //If the day isn't valid, like the 10046th or something, this will open the input box again and have a \e so the dialogue below doesn't play.
	{
		"\0\s[0]That's not a valid day. Try again."
		--
		"\![open,inputbox,OnUserBornDay,-1]\e"
	}	
}

//--birthchecked
//This is the new envelope you saw me point out above.

birthchecked
{
	if userbirthday == dayslot //if today is the user's birthday
		{
			"\0\s[0]It's the user's birthday.\w8\1\s[10]Happy birthday, %(username).\e"
		}
	elseif userbirthday == "12month 25day"
		{
			"\0\s[0]The user was born on Christmas.\w8\1\s[10]Happy birthday, %(username). \w5AND JESUS!\w8\0\s[0]\n\n[half]We're supposed to be a blank template, knock it off before we get in trouble.\e"
		}
	else //if the birthday is not listed above.
		{
			"\0\s[0]Your birthday is %(bornmonthprint) %(borndayprint). We'll remember.\e"
		}
}

//--Talk Rate
//This is when the user chooses to adjust the talk rate in the config menu. The talk rate is how often the ghost will read some dialogue from aitalk.dic.

Select.talkinter
 	{
 		"\1\b2How often should we speak?\w8\0Right now it's %(talktime)./
		//note the new envelope here for %(talktime). You'll see it again in a second. You can replace the dialogue above with whatever you like, just make sure it ends with ./ and not \e.
 		\0\n\n[half]/
 		\![*]\q[Every 30 seconds,TALKMAX]\n/
		\![*]\q[Every minute,TALKHIGH]\n/
 		\![*]\q[Every 3 minutes,TALKNORMAL]\n/
		\![*]\q[Every 5 minutes,TALKPOOR]\n/
 		\![*]\q[Don't speak,TALKNONE]\n\n[half]/
		\![*]\q[Don't change,TalksTill]\e"
	}

//If the user doesn't want to change the rate.
Select.TalksTill
	{
		"\p[2]Change you're mind?\0I don't mind talking the same amount.\e"
	}

//If the user chooses every 30 seconds.
Select.TALKMAX
	{
 		talktime = "30 seconds" //this sets up the envelope properly
		aitalkinterval = 30 //this changes the actual rate
		
 		"\1Sounds good to me!\0Of course it does...\e"
	}

//If the user chooses every minute.
Select.TALKHIGH
	{
 		talktime = "minute"
		aitalkinterval = 60
		
 		"\0Every minute it is!\e"
	}

//If the user chooses every three minutes.
Select.TALKNORMAL
	{
 		talktime = "3 minutes"
		aitalkinterval = 180
		
 		"\p[2]3 minutes sounds good to me!\w5\1I meeeaaaann.\w2.\w2.\w4 It \f[italic,true]could\f[italic,false] be more often.\w2.\w2.\w6\0You're the only one asking for that though.\w2.\w2.\e"
	}

//If the user chooses every five minutes. This is the default rate the ghost will start up in, as seen in bootend.dic.
Select.TALKPOOR
	{
 		talktime = "5 minutes"
		aitalkinterval = 300
		
 		"\0We will speak every five minutes.\e"
	}

//If the user chooses to not have them speak.
Select.TALKNONE
	{
 		talktime = "...actually we won't talk at all."
		aitalkinterval = 0
		
		"\1\s[10]We won't say a thing!\w4\0Yeah, right.\w5\p[2]I'll believe it when I see it.\e"

		"\p[2]Me and Wakko won't talk, but good luck getting Yakko not to.\w2\1Hey! I'll have you know I'm perfectly capable of not talking."
	}

	
//This is when the user chooses to change their name. It's linked to the functions outlined in nameteach.dic.
Select.TEACHNAME
	{
		OnTeachName	
	}

	
//If the user selects cancel in a menu.
Select.CANCEL
	{
		"\0Okay.\e"
 	}

	
//Going back to the MenuRun function we finished above, this is if someone selects the Functions option, linked to dosomething. As you can see, it ends up as Select.dosomething.

Select.dosomething
{
 		"\0\s[0]\b2What can we help you with?\n\n"
 		"\0\s[0]\b2Will I get a treat if I help?\n\n"
		"\0\s[0]\b2Yeah?\n\n"
		--
 		"\![*]\q[Open Calendar,CALENDAR]\n/
		\![*]\q[Check Email,MailCheck]\n/
		\![*]\q[Open SSP Preferences,sspconfig]\n/
		\![*]\q[Empty Recycle Bin,ChoiceGarbagecan]\n/
		\![*]\q[System Information,ChoiceSystemInformation]\n\n/
 		\![*]\q[Back to main menu,BACKTOMENU]\e"
}

//When the user clicks Open Calender. This will open the calender function of SSP, an in-built feature that you don't have to worry about in the least.
Select.CALENDAR
{
	"\1I can show you mine!\w5\0The one with all the scribbles?\w3\1No, the scibble-less one. Obviously.\w8\![open,calendar]\e"
}

//When the user chooses SSP preferences. This will open the same window that shows up if you right click your ghost and go to Utilities - Preferences.
Select.sspconfig
{
	"\0\s[0]Opening the SSP config menu.\w8\![open,configurationdialog]\e"
}

//If the user chooses to empty the recycle bin.
Select.ChoiceGarbagecan
{
	_gomicheck = FUNCTIONEX("gomi.dll", "-n")	//for those curious, it's referencing a SAORI, which are optional user-made plug-in dlls you can download to enable different functions on your ghost. gomi.dll is one included with this template.
	
	if _gomicheck == 0 //If the recycle bin is empty.
	{
		"\0\s[1]Okie dokie!\w8\w8\s[-1]\w8\w8\w8\w8\w8\s[0]\nIt was empty.\e"

		"\1\s[11]On it! \w8\w8\s[-1]\w8\w8\w8\w8\w8\![raise,OnGarbagecanEmpty]\w9\w9\w9\s[10]\nYour recycle bin is empty, %(username).\w8 \s[12]I hope you weren't just trying to get rid of me.\e"

		"\p[2]\s[24]I'll do it!\w8\w8\s[-1]\c\w8\w8\w8\w8\w8\![raise,OnGarbagecanEmpty]\w9\w9\w9\s[26]It was empty! Make sure there's actually something to throw away before you ask someone to empty it next time!\e"

		"\0I got it!\w8\w5\s[-1]\p[2]\s[26]Hey! \w4I wanted to do it!\w8\w5\s[-1]\w8\w8\w8\w8\w8\0\s[1]\nYay! I won!\w4\p[2]\s[24]\nNot that it mattered, the recycle bin was empty.\e"
	}
	elseif _gomicheck >= 1 //If the recycle bin is full.
	{
		"\0\s[1]Okie dokie!\w8\w8\s[-1]\c\w8\w8\w8\w8\w8\![raise,OnGarbagecanEmpty]\w9\w9\w9\cAll done!\e"

		"\1\s[11]On it!\w8\w8\s[-1]\c\w8\w8\w8\w8\w8\![raise,OnGarbagecanEmpty]\w9\w9\w9\cAll clean!\e"

		"\p[2]\s[24]I'll do it!\w8\w8\s[-1]\c\w8\w8\w8\w8\w8\![raise,OnGarbagecanEmpty]\w9\w9\w9\cEmptied!\e"

		{
			"\p[2]\s[25]Gladly!\w7\s[-1]\0\s[5]Wait I wanted to do that!\w7\s[-1]\1\s[10]Well if they're going, I'm going too!\w7\s[-1]\w8\w8\w8\w8\w8\![raise,OnGarbagecanEmpty]\w9\w9\w9"
			--
			"\p[2]\s[24]\cAll done!\w8\0\s[5]\cAw man, I couldn't catch up...\w5\1\s[19]She's just too fast!\e"

			"\0\s[0]\cI win!\w8\p[2]\s[24]\cOnly because I tripped on the desktop shortcuts! You need to clean up your screen, %(username).\w3\1\s[10]\cI'll say.\w3\s[12] I almost got lost coming back!\e"

			"\0\s[0]\p[2]\s[24]\cAll done!\w8\0\s[0]\cWe did it \w2\s[1]together!\w5\1\s[10]And I'm back too!\e"
		}
		
		//Somewhat similar to the above, but notice the \![raise],OnGarbagecanEmpty] tag in there. That's what actually empties the recycle bin, so make sure you don't delete that tag.
	}
}


//This is for the SAORI, don't touch it.
OnGarbagecanEmpty
{
	_gomi = FUNCTIONEX("gomi.dll", "-f")
}

//This is when the user chooses System Information. Don't touch any of this either. For those curious though, note that this is done using another SAORI, this time saori_cpuid.dll.
Select.ChoiceSystemInformation
{
	os_name = FUNCTIONEX("saori_cpuid.dll","os.name")
	os_version = FUNCTIONEX("saori_cpuid.dll","os.version")
	cpu_name = FUNCTIONEX("saori_cpuid.dll","cpu.name")
	cpu_clockex = FUNCTIONEX("saori_cpuid.dll","cpu.clockex")
	"/
	\0\s[0]\b[2]/
	\_q\f[bold,1]OS:\f[bold,0] %(os_name) %(os_version)\n/
	\f[bold,1]Memory Load:\f[bold,0] %(memoryload)%\n/
	\f[bold,1]Physical Memory:\f[bold,0] %(memoryavailphys) KB\n/
	\f[bold,1]CPU:\f[bold,0] %(cpu_name) %(cpu_clockex)MHz\n/
	\f[bold,1]Ukagaka:\f[bold,0] %property[baseware.version]/
	\e"
}
	
//This is when the user chooses to check their email. The rest of this function plays out in etc.dic.
Select.MailCheck
	{
		"\![biff]\e"
	}


	
//---- OnChoiceTimeout ---------------------------------------------------------
//If the user is given a menu with choices, but doesn't pick one of the choices, it will eventually time out.

OnChoiceTimeout
 {
	"\0\s[3]Uh... no answer?\e"
	"\1\s[10]Yunno most people consider it rude to leave someone hanging like that.\e"
 }


 
//---- Secondary Character Menu ---------------------------------------------------------
//This section has to do with the menu for the second character.

OpenKeroMenu
 {
	"\1\s[10]\b[2]"
	"\1\s[10]\b[2]"
	"\1\s[10]\b[2]"
 	--
	"Ye-es?"
 	"What can I help you with?"
 	"Need something explained?"
 	"Did you need something?"
 	--
	"\n\n[half]/
	\![*]\q[Talk,yakkoTalk]\n\n[half]/
	\![*]\q[Info,talkInfo]\n\n[half]/
	\![*]\q[Nothing,keroCANCEL]\e"
}

//If you click cancel.
Select.keroCANCEL
{
	"\1Alrighty.\e"
	"\1Try asking my siblings, they might be able to help.\e"
}

//Talk with only Yakko
Select.yakkoTalk
{
	"\1\s[10]Did you hear about the nut-thief that got caught last week?\w7 I heard he was very \w2squirrley!\e"
	"\1\s[10]Once we got the whole studio in a water balloon fight.\w5 \s[21]Of course, it was mostly us throwing the balloons, \s[11]but it was still fun!\e"
}

Select.talkInfo
{
	"\1\b[2]\_q" 
	--
	"\f[bold,1]Yakko Pets:\f[bold,0] %(yakPets)\n\n[half]"
	--
	"\f[bold,1]Wakko Pets:\f[bold,0] %(wakPets)\n\n[half]"
	--
	"\f[bold,1]Dot Pets:\f[bold,0] %(dotPets)\n\n[half]"
	--
	"\![*]\q[All done!,null]\e"
	"\![*]\q[Thanks!,null]\e"
}


//---- Tertiary Character Menu ---------------------------------------------------------
//This section has to do with the menu for the second character.

OpenSanMenu
 {
	"\p[2]\s[24]\b[2]"
 	--
	"What is it?"
 	"I bet I can help you, what do you need?"
 	"Ye-es?"
 	--
	"\n\n[half]/
	\![*]\q[Talk,dotTalk]\n/
	\![*]\q[Nothing,sanCANCEL]\e"
}

Select.sanCANCEL
{
	"\p[2]\s[24]Well, if you're sure.\e"
	"\p[2]\s[24]Alrighty then.\e"
	"\p[2]\s[24]Maybe I can help next time?\e"
}

//Talk with only Dot
Select.dotTalk
{
	"\p[2]\s[24]What would you say to us having a girl's night, just you and me?\e"
}